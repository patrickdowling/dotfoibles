function zsh_autoload_functions() {
	if [ -d "$1" ] ; then
		fpath+=$1
		local funs=($1/*)
		for fun in ${funs}; do
			autoload -Uz ${fun:t}
		done
	fi
}

function zsh_detect_os() {
	case "$(uname -s)" in
		Darwin*) echo "macos" ;;
		Linux*) echo "linux" ;;
		*) ;;
	esac
}

function zsh_file_source() {
	[ -f "$1" ] && source "$1"
}

function zsh_path_add() {
	[ ! -d "$1" ] || path=($path $1)
}

function zsh_path_head() {
	[ ! -d "$1" ] || path=($1 $path)
}

function zsh_fpath_add() {
	[ ! -d "$1" ] || fpath=($fpath $1)
}

zsh_autoload_functions "$ZDOTDIR/functions"

typeset -U ZSH_PLUGINS
ZSH_PLUGINS=()

# Add plugin from git repo
function zsh_plugins_add() {
	local plugin_name=$(echo $1 | cut -d "/" -f 2)
	local plugin_dir="$ZSH_PLUGIN_DIR/$plugin_name"
	ZSH_PLUGINS+="$plugin_name"

	[ -d "$plugin_dir" ] || git clone --depth 1 "https://github.com/$1.git" "$plugin_dir"
	case $2 in
		false) return ;; # don't source anything
		"") source "$plugin_dir/$plugin_name.plugin.zsh" || source "$plugin_dir/$plugin_name.zsh" ;;
		*) source "$plugin_dir/$2" ;;
	esac
}

# ls on change of current working directory
(( $+functions[add-zsh-hook] )) || autoload -Uz add-zsh-hook
add-zsh-hook chpwd ls_on_chwd
